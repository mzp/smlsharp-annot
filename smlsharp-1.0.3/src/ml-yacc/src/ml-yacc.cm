(* Modified by Katsuhiro Ueno on 2011-Nov-25 to port ml-yacc to SML#. *)
Library
  structure ExportParseGen
is

# if defined (NEW_CM)
  $/basis.cm
  $/ml-yacc-lib.cm
# else
  ../lib/ml-yacc-lib.cm
# endif

(*
(* Ueno (2011-11-25): rearranged for MLton. *)
  sigs.sml

  utils.sig
*)
  utils.sig
  sigs.sml
  hdr.sml

(* Ueno (2011-11-25): added the following condition for MLton. *)
#if defined(SMLNJ_VERSION)

(* During installation we rely on pre-generated files
 * to avoid certain chicken-and-egg problems. *)
#if defined(NO_ML_YACC)
  yacc.grm.sig
  yacc.grm.sml
#else
  yacc.grm
#endif

(* During installation we rely on pre-generated files
 * to avoid certain chicken-and-egg problems. *)
#if defined(NO_ML_LEX)
  yacc.lex.sml
#else
  yacc.lex
#endif

#else (* for MLton *)
  yacc.grm.sig
  yacc.grm.sml
  yacc.lex.sml
#endif (* SMLNJ_VERSION *)

  parse.sml

  utils.sml
  grammar.sml
  core.sml
  coreutils.sml
  graph.sml
  look.sml
  lalr.sml
  mklrtable.sml
  mkprstruct.sml
  shrink.sml
  verbose.sml

  absyn.sig
  absyn.sml
  yacc.sml
  link.sml

  export-yacc.sml

(* Ueno (2011-11-25): added the following for SML# and MLton. *)
#if defined(SMLNJ_VERSION)
#else
  ml-yacc.sml
#endif
